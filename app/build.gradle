apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.allever.security.photo.browser"
        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //默认渠道为官网
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "offical"]
    }

    //友盟多渠道
    flavorDimensions "mark"
    productFlavors {
        google {}
        xiaomi {}
        baidu {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }


    signingConfigs {
        release {
            storeFile file("${rootProject.ext.signingStoreFile}")
            storePassword "${rootProject.ext.signingStorePassword}"
            keyAlias "${rootProject.ext.signingKeyAlias}"
            keyPassword "${rootProject.ext.signingKeyPassword}"
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all {
        variant ->
            if (variant.buildType.name.equals('release')) {
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = "${defaultConfig.applicationId}_(Release)_V${defaultConfig.versionName}_C${defaultConfig.versionCode}_${variant.flavorName}_(Build${releaseTime()}).apk"
                        output.outputFileName = new File(fileName)
                        //com.allever.security.photo.browser_(Release)_V1.0_C1_(Build201908251506).apk
                    }
                }
            }
    }
}


static def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("Asia/Chongqing"))
}


//引入aar包需加
repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //android
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //local
    implementation project(':permission')
    implementation project(':common')
    implementation project(':umeng')
    implementation(name: "Toolkit-1.1", ext: 'aar')

    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'

}
